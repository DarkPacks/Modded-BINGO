river = forBiomes("minecraft:river")
void = forBiomes("minecraft:void")
river.registerGenBiomeRep("minecraft:void")

river.set("isSpawnBiome", false)
void.set("isSpawnBiome", false)

glowshroom = forBlock("biomesoplenty:mushroom")
glowshroom.setProperty("variant", "glowshroom")
void.registerGenBlockRep("biomesoplenty:mushroom", glowshroom)

miners_delight = forBlock("biomesoplenty:flower_1")
miners_delight.setProperty("variant", "miners_delight")
void.registerGenBlockRep("biomesoplenty:flower_1", miners_delight)

# PlacementStage{ BIOME_BLOCKS, PRE_POPULATE, POST_POPULATE, PRE_DECORATE, PRE_ORES, POST_ORES, POST_DECORATE; }

river.removeDecoration("FLOWERS")
river.removeDecoration("SHROOM")

void.removeDecoration("FLOWERS")
void.removeDecoration("SHROOM")

void.removeAllSpawns("CREATURE")
void.removeAllSpawns("MONSTER")
void.removeAllSpawns("AMBIENT")
void.removeAllSpawns("WATER_CREATURE")

Tweaker.setPlacementStage("BIOME_BLOCKS")
void.registerGenBlockRep("minecraft:grass", "minecraft:air")
void.registerGenBlockRep("minecraft:stone", "minecraft:air")
void.registerGenBlockRep("minecraft:dirt", "minecraft:air")
void.registerGenBlockRep("minecraft:flowing_water", "minecraft:air")
void.registerGenBlockRep("minecraft:water", "minecraft:air")

Tweaker.setPlacementStage("PRE_POPULATE")
void.registerGenBlockRep("minecraft:flowing_lava", "minecraft:air")
void.registerGenBlockRep("minecraft:lava", "minecraft:air")
void.registerGenBlockRep("minecraft:bedrock", "minecraft:air")
void.registerGenBlockRep("minecraft:flowing_water", "minecraft:air")

Tweaker.setPlacementStage("POST_POPULATE")
void.registerGenBlockRep("minecraft:stone", "minecraft:air")
void.registerGenBlockRep("minecraft:flowing_water", "minecraft:air")

Tweaker.setPlacementStage("POST_DECORATE")
void.registerGenBlockRep("minecraft:flowing_water", "minecraft:air")
void.registerGenBlockRep("minecraft:water", "minecraft:air")
# void.registerGenBlockRep("minecraft:gold_block", "minecraft:air")

void.registerGenBlockRep(glowshroom, "minecraft:air")
void.registerGenBlockRep(miners_delight, "minecraft:air")

# river.removeDecoration(["BIG_SHROOM", "CACTUS", "CLAY", "DEAD_BUSH", "DESERT_WELL", "LILYPAD", "FLOWERS", "FOSSIL", "GRASS", "ICE", "LAKE_WATER", "LAKE_LAVA", "PUMPKIN", "REED", "ROCK", "SAND", "SAND_PASS2", "SHROOM", "TREE", "CUSTOM", "SAND_PASS2"])

# removeGenerator "DUNGEON", "FIRE", "GLOWSTONE", "ICE", "LAKE", "LAVA", "NETHER_LAVA", "NETHER_LAVA2", "NETHER_MAGMA", "ANIMALS", and "CUSTOM".